title: Start
---
<<camera Title>>
<<fadeIn>>
<<wait 2>>
<<fadeOut>>
<<camera Corridor>>
<<fadeIn>>
Player: Another day in Space Fleet. Might go have a chat...
// pick a person to speak to
-> Go see the Engineer as per orders
    <<jump TalkToEngineer>>
-> Meet up with your friend
    <<jump TalkToCrewmate>>
-> Go and talk to the Captain
    <<jump TalkToCaptain>>
===
title: TalkToEngineer
---
<<fadeOut>>
<<place Engineer Corridor Left>>
<<fadeIn>>
Engineer: Hello! I am the Engineer.
<<jump BridgeEnding>>
===
title: TalkToCrewmate
---
<<fadeOut>>
<<place Crewmate Corridor Center>>
<<fadeIn>>
Crewmate: Hello! I am your Crewmate.
<<jump BridgeEnding>>
===
title: TalkToCaptain
---
<<fadeOut>>
<<place Captain Corridor Right>>
<<fadeIn>>
Captain: Hello! I am the Captain.
Player: I want to talk to go on more missions.
Captain: Do you think you are ready?
-> Yes
    <<set $away_mission_readiness += 1>>
    // if so, ask again
    Captain: Really?
    -> ...yes?
        <<set $away_mission_readiness += 1>>
    -> Actually, no.
-> No
// now go to the ending
<<jump BridgeEnding>>
===
title: BridgeEnding
---
// everyone reports to the bridge
<<fadeOut>>
<<camera Bridge>>
<<place Engineer Bridge Left>>
<<place Captain Bridge Right>>
<<pose Engineer hands-on-hips>>
<<expression Engineer angry>>
<<pose Captain hand-at-mouth>>
<<expression Captain angry>>
<<fadeIn>>
Captain: Pirates!
Player: Oh no!
// now change who goes depending on player actions
<<if $away_mission_readiness < 2>>
    <<place Crewmate Bridge Center>>
    Captain: Crewmate, go deal with those pirates!
    <<expression Crewmate happy>>
    <<pose Captain pointing>>
    Crewmate: Yes, Captain.
<<else>>
    <<pose Captain pointing>>
    <<expression Captain smirk>>
    Captain: Player, you were just telling me how ready for this you are. Go deal with those pirates!
    Player: Hooray!
<<endif>>
<<fadeOut>>
THE END
===